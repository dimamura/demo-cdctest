buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
       classpath 'org.springframework.cloud:spring-cloud-contract-gradle-plugin:2.2.5.RELEASE'
    }
}

plugins {
    id 'java'
    id 'io.spring.dependency-management' version '1.0.10.RELEASE'
}

apply plugin: 'spring-cloud-contract'
apply plugin: 'maven'
apply plugin: 'eclipse'

group = 'jp.co.ogis_ri.rd.nautible.cdctest'
version = '0.1.0-SNAPSHOT'
sourceCompatibility = '1.8'

jar {
    manifest {
        attributes(
            'Implementation-Title': project.name,
            'Implementation-Version': project.version,
            'Created-By': "Gradle ${gradle.gradleVersion}",
            'Built-By': "${System.properties['user.name']}",
            'Build-Timestamp': new java.text.SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
            'Build-Jdk': "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
            'Build-OS': "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}"
        )
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

sourceSets.test {
    java.srcDirs = ['src/test/java', 'src/test/gen-java']
    resources.srcDirs = ['src/test/resources', 'src/test/gen-resources']
}

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {

    testImplementation('org.springframework.boot:spring-boot-starter-test:2.3.7.RELEASE') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    testImplementation 'org.springframework.cloud:spring-cloud-starter-contract-verifier:2.2.5.RELEASE'

}

dependencyManagement {
    imports {
        mavenBom 'org.springframework.cloud:spring-cloud-dependencies:Hoxton.SR9'
    }
}

contracts {
    testFramework = 'JUNIT5'
    testMode = 'EXPLICIT'
    generatedTestSourcesDir = project.file('src/test/gen-java')
    generatedTestResourcesDir = project.file('src/test/gen-resources')
    basePackageForTests = 'jp.co.ogis_ri.rd.nautible.cdctest.awssqs.contracttest'
    packageWithBaseClasses = 'jp.co.ogis_ri.rd.nautible.cdctest.awssqs.contracttest'
}

test {
    useJUnitPlatform()
    systemProperty 'file.encoding', 'utf-8'
}

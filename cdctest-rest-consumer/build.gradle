plugins {
    id 'org.springframework.boot' version '2.3.7.RELEASE'
    id 'io.spring.dependency-management' version '1.0.10.RELEASE'
    id 'java'
}

group = 'jp.co.ogis_ri.rd.nautible.cdctest'
version = '0.1.0-SNAPSHOT'
sourceCompatibility = '1.8'

jar {
    manifest {
        attributes(
            'Implementation-Title': project.name,
            'Implementation-Version': project.version,
            'Created-By': "Gradle ${gradle.gradleVersion}",
            'Built-By': "${System.properties['user.name']}",
            'Build-Timestamp': new java.text.SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
            'Build-Jdk': "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
            'Build-OS': "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}"
        )
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {

    compileOnly 'org.projectlombok:lombok:1.18.16'
    annotationProcessor 'org.projectlombok:lombok:1.18.16'

    implementation 'org.springframework.boot:spring-boot-starter:2.3.7.RELEASE'
    implementation 'org.springframework:spring-web:5.2.12.RELEASE'

    testImplementation('org.springframework.boot:spring-boot-starter-test:2.3.7.RELEASE') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }

    testImplementation 'org.springframework.cloud:spring-cloud-starter-contract-stub-runner:2.2.5.RELEASE'

    testImplementation('jp.co.ogis_ri.rd.nautible.cdctest:cdctest-rest-api:0.1.0-SNAPSHOT:stubs') {
        transitive = false
    }

}

dependencyManagement {
    imports {
        mavenBom 'org.springframework.cloud:spring-cloud-dependencies:Hoxton.SR9'
    }
}

test {
    useJUnitPlatform()
    systemProperty 'file.encoding', 'utf-8'
}
